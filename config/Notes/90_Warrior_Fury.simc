## Fury Warrior
## July 5, 2021

## Changes:
## - Added Pummel.
## - Disabled movement / Heroic Leap call (not needed for in-game context).
## - Moved Bladestorm and Dragon Roar above Bloodthirst to improve frequency of recommendations (sims roughly equal).
## - Added Battle Shout to precombat APL.
## - Added Use Items.
## - Tweak for Whirlwind before level 37.
## - Removed Essences.
## - Don't use Siegebreaker if it's already up (Deathmaker).
## - Setup fake sequences for Signet opener.

## Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/battle_shout
actions.precombat+=/recklessness,if=!runeforge.signet_of_tormented_kings.equipped

## Executed every time the actor is available.
# Charge if we're long past our opener, or we don't need the opener combo for Signet (non-Venthyr).
actions=charge,if=time>8|talent.double_time.enabled|active_enemies>1|covenant.venthyr|!runeforge.signet_of_tormented_kings.equipped|cooldown.recklessness.remains>gcd
actions+=/pummel
actions+=/variable,name=execute_phase,value=talent.massacre&target.health.pct<35|target.health.pct<20|target.health.pct>80&covenant.venthyr
actions+=/variable,name=unique_legendaries,value=runeforge.signet_of_tormented_kings|runeforge.sinful_surge|runeforge.elysian_might
## This is mostly to prevent cooldowns from being accidentally used during movement.
## actions+=/run_action_list,name=movement,if=movement.distance>5
## actions+=/heroic_leap,if=(raid_event.movement.distance>25&raid_event.movement.in>45)
## actions+=/sequence,if=active_enemies=1&covenant.venthyr.enabled&runeforge.signet_of_tormented_kings.equipped,name=BT&Reck:bloodthirst:recklessness
# Emulates initial Bloodthirst sequence for single-target + Signet.
actions+=/bloodthirst,line_cd=10,if=time<8&active_enemies=1&runeforge.signet_of_tormented_kings.equipped&(!covenant.venthyr|cooldown.recklessness.remains<=gcd)
# Emulates initial Recklessness sequence for single-target + Venthyr + Signet.
actions+=/recklessness,line_cd=10,if=time<8&active_enemies=1&covenant.venthyr.enabled&runeforge.signet_of_tormented_kings.equipped&prev.bloodthirst
## actions+=/sequence,if=active_enemies=1&!covenant.venthyr.enabled&runeforge.signet_of_tormented_kings.equipped,name=BT&Charge:bloodthirst:heroic_charge
# Emulates initial Heroic Leap > Charge sequence for single-target + non-Venthyr + Signet.
actions+=/heroic_leap,line_cd=10,if=settings.heroic_charge&time<5&active_enemies=1&!covenant.venthyr.enabled&runeforge.signet_of_tormented_kings.equipped&prev.bloodthirst&cooldown.charge.remains<0.5
# Emulates initial Heroic Leap > Charge sequence for single-target + non-Venthyr + Signet.
actions+=/charge,line_cd=10,if=settings.heroic_charge&time<5&active_enemies=1&!covenant.venthyr.enabled&runeforge.signet_of_tormented_kings.equipped&prev.heroic_leap
actions+=/potion
actions+=/rampage,if=cooldown.recklessness.remains<3&talent.reckless_abandon.enabled
actions+=/conquerors_banner,if=buff.recklessness.down&debuff.siegebreaker.down|fight_remains<50
actions+=/recklessness,if=runeforge.sinful_surge&gcd.remains=0&(variable.execute_phase|(target.time_to_pct_35>40&talent.anger_management|target.time_to_pct_35>70&!talent.anger_management))&(spell_targets.whirlwind=1|buff.meat_cleaver.up)
actions+=/recklessness,if=runeforge.elysian_might&gcd.remains=0&(cooldown.spear_of_bastion.remains<5|cooldown.spear_of_bastion.remains>20)&((buff.bloodlust.up|talent.anger_management.enabled|raid_event.adds.in>10)|target.time_to_die>100|variable.execute_phase|target.time_to_die<15&raid_event.adds.in>10)&(spell_targets.whirlwind=1|buff.meat_cleaver.up)
actions+=/recklessness,if=!variable.unique_legendaries&gcd.remains=0&((buff.bloodlust.up|talent.anger_management.enabled|raid_event.adds.in>10)|target.time_to_die>100|variable.execute_phase|target.time_to_die<15&raid_event.adds.in>10)&(spell_targets.whirlwind=1|buff.meat_cleaver.up)
actions+=/recklessness,use_off_gcd=1,if=runeforge.signet_of_tormented_kings.equipped&prev_gcd.1.rampage&((buff.bloodlust.up|talent.anger_management.enabled|raid_event.adds.in>10)|target.time_to_die>100|variable.execute_phase|target.time_to_die<15&raid_event.adds.in>10)&(spell_targets.whirlwind=1|buff.meat_cleaver.up)
actions+=/whirlwind,if=level>36&(spell_targets.whirlwind>1&!buff.meat_cleaver.up|raid_event.adds.in<gcd&!buff.meat_cleaver.up)
actions+=/use_items
actions+=/blood_fury
actions+=/berserking,if=buff.recklessness.up
actions+=/lights_judgment,if=buff.recklessness.down&debuff.siegebreaker.down
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks,if=buff.recklessness.down&debuff.siegebreaker.down&buff.enrage.up
actions+=/call_action_list,strict=1,name=aoe,if=spell_targets.whirlwind>1
actions+=/run_action_list,name=single_target

actions.aoe=cancel_buff,name=bladestorm,if=gcd.remains=0&soulbind.first_strike&buff.first_strike.remains&buff.enrage.remains<gcd
actions.aoe+=/ancient_aftershock,if=buff.enrage.up&cooldown.recklessness.remains>5
actions.aoe+=/spear_of_bastion,if=buff.enrage.up&rage<40
actions.aoe+=/bladestorm,interrupt_if=gcd.remains=0&soulbind.first_strike&buff.first_strike.remains&buff.enrage.remains<gcd,if=buff.enrage.up&spell_targets.whirlwind>2
actions.aoe+=/condemn,if=(buff.enrage.up|buff.recklessness.up&runeforge.sinful_surge)&variable.execute_phase
actions.aoe+=/siegebreaker
actions.aoe+=/rampage
actions.aoe+=/spear_of_bastion,if=buff.enrage.up&cooldown.recklessness.remains>5
actions.aoe+=/bladestorm,if=buff.enrage.remains>gcd*2.5

actions.single_target+=/raging_blow,if=runeforge.will_of_the_berserker.equipped&buff.will_of_the_berserker.remains<gcd
actions.single_target+=/crushing_blow,if=runeforge.will_of_the_berserker.equipped&buff.will_of_the_berserker.remains<gcd
actions.single_target+=/cancel_buff,name=bladestorm,if=spell_targets.whirlwind=1&gcd.remains=0&(talent.massacre.enabled|covenant.venthyr.enabled)&variable.execute_phase&(rage>90|!cooldown.condemn.remains)
actions.single_target+=/condemn,if=(buff.enrage.up|buff.recklessness.up&runeforge.sinful_surge)&variable.execute_phase
actions.single_target+=/condemn,if=buff.enrage.up&variable.execute_phase
actions.single_target+=/siegebreaker,if=debuff.siegebreaker.down&(spell_targets.whirlwind>1|raid_event.adds.in>15)
actions.single_target+=/rampage,if=(buff.recklessness.up|buff.memory_of_lucid_dreams.up)|(buff.enrage.remains<gcd|rage>90)|buff.frenzy.remains<1.5
actions.single_target+=/condemn
actions.single_target+=/ancient_aftershock,if=buff.enrage.up&(spell_targets.whirlwind>1|raid_event.adds.in>75)
actions.single_target+=/spear_of_bastion,if=buff.enrage.up&(spell_targets.whirlwind>1|raid_event.adds.in>45)
actions.single_target+=/execute
actions.single_target+=/bladestorm,if=buff.enrage.up&(!buff.recklessness.remains|rage<50)&(spell_targets.whirlwind=1&raid_event.adds.in>45|spell_targets.whirlwind=2)
actions.single_target+=/bloodthirst,cycle_targets=1,if=buff.enrage.down|conduit.vicious_contempt.rank>5&target.health.pct<35
actions.single_target+=/bloodbath,if=buff.enrage.down|conduit.vicious_contempt.rank>5&target.health.pct<35&!talent.cruelty.enabled
actions.single_target+=/dragon_roar,if=buff.enrage.up&(spell_targets.whirlwind>1|raid_event.adds.in>15)
actions.single_target+=/onslaught
actions.single_target+=/whirlwind,if=buff.merciless_bonegrinder.up&spell_targets.whirlwind>3
actions.single_target+=/raging_blow,if=charges=2|buff.recklessness.up&variable.execute_phase&talent.massacre.enabled
actions.single_target+=/crushing_blow,if=charges=2|buff.recklessness.up&variable.execute_phase&talent.massacre.enabled
actions.single_target+=/bloodthirst,cycle_targets=1,if=buff.enrage.down
actions.single_target+=/bloodthirst
actions.single_target+=/bloodbath
actions.single_target+=/raging_blow
actions.single_target+=/crushing_blow
actions.single_target+=/whirlwind